apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'clojure'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'idea'

aotCompile = true
warnOnReflection = true

repositories {
  mavenCentral()
  clojarsRepo()
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.fork = true
    scalaCompileOptions.unchecked = true

    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxPermSize=512m']
    }
}

dependencies {
    compile project(':language-adaptors:rxjava-clojure')
    compile project(':language-adaptors:rxjava-groovy')
    compile project(':language-adaptors:rxjava-scala')
    compile project(':language-adaptors:rxjava-jruby')

    provided 'junit:junit:4.10'
    provided 'org.mockito:mockito-core:1.8.5'

	// clojure
    compile 'clj-http:clj-http:0.6.4' // https://clojars.org/clj-http
	
	// groovy
    groovy 'org.codehaus.groovy:groovy-all:1.8+'
	
	// scala
    scalaTools 'org.scala-lang:scala-compiler:2.10+'
    scalaTools 'org.scala-lang:scala-library:2.10+'
    provided 'org.scalatest:scalatest_2.10:1.9.1'
}

/* 
 * Add Counterclockwise and include 'provided' dependencies
 */
eclipse {
  project {
    natures "ccw.nature"
  }
  classpath {
    plusConfigurations += configurations.provided
    downloadSources = true
    downloadJavadoc = true
  }
}

/* 
 * Adds clojuresque to the (build) classpath
 */
buildscript {
  repositories { maven { url "http://clojars.org/repo" } }
  dependencies { classpath "clojuresque:clojuresque:1.5.4" }
}

